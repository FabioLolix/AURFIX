# Maintainer: Steffen Hansen <steffengrundsoe@gmail.com>
pkgname=quickemu
pkgver=4.9.6
pkgrel=2
pkgdesc="Quickly create and run optimised Windows, macOS and Linux desktop virtual machines."
arch=(any)
url="https://github.com/quickemu-project/quickemu"
license=(MIT)
depends=(qemu-desktop jq python cdrtools usbutils spice-gtk swtpm xorg-xrandr zsync edk2-ovmf)
optdepends=('quickgui: graphical user interface')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/quickemu-project/quickemu/archive/refs/tags/$pkgver.tar.gz"
        "quickemu-4M-OVMF.patch::https://github.com/quickemu-project/quickemu/commit/d0ae72a2510f52ae4a9181d7c65f2c69636ca298.patch")
sha256sums=('796a047b0bfabb91eb143e5422ff74f5d2812f47120ed6b34eaf82bbdfe3e2a3'
            '50fe183a23cadad32a99b7b3c0de0aa4e36fd3b2e53fc4aa3cb059d87cc95275')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../quickemu-4M-OVMF.patch
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd docs
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
