# Maintainer: Steffen Hansen <steffengrundsoe@gmail.com>
pkgname=quickemu
pkgver=4.9.7
pkgrel=2
pkgdesc="Quickly create and run optimised Windows, macOS and Linux desktop virtual machines."
arch=(any)
url="https://github.com/quickemu-project/quickemu"
license=(MIT)
depends=(qemu-desktop jq python cdrtools usbutils spice-gtk swtpm xorg-xrandr zsync edk2-ovmf)
optdepends=('quickgui: graphical user interface')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/quickemu-project/quickemu/archive/refs/tags/$pkgver.tar.gz"
        "quickemu-qemu-version-detection.patch::https://github.com/quickemu-project/quickemu/pull/1640/commits/8fad1b9370bf4f825614fa161a41e5e5815665f8.patch")
sha256sums=('38a93301a2b233bc458c62d1228d310a9c29c63c10d008c2905029ca66188606'
            '005b198f0d4cd06037eed3036e682f89c9311e1be68d9577702ed788998960a9')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../quickemu-qemu-version-detection.patch
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd docs
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}
