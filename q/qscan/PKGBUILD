# Maintainer: Daniel Mensinger <daniel@mensinger-ka.de>

_pkgname=qScan
pkgname=qscan
pkgver=1.0.4
pkgrel=1
pkgdesc="QT document scanner using SANE"
arch=('i686' 'x86_64' 'aarch64' 'armv7h')
url="https://github.com/mensinda/qScan"
license=('Apache')
depends=(sane qt5-base imagemagick spdlog)
makedepends=(meson nlohmann-json)
_enumGenver=1.1.2
source=(
    "qscan-${pkgver}.tar.gz::https://github.com/mensinda/$_pkgname/archive/refs/tags/v${pkgver}.tar.gz"
    "qscan-enumGen-${_enumGenver}.tar.gz::https://github.com/mensinda/enumGen/archive/refs/tags/v${_enumGenver}.tar.gz" # Compile time code generator
    "qscan-gcc13-fix.patch::https://github.com/mensinda/qScan/commit/887c797ae104c4832d2701e426540e53c7bd0882.patch"
)
sha256sums=('579e4ceef52df8393d2bcd2fd30f0eabab4076b71b8542d9192630b953f8819e'
            '987e608f52e651e1c90514893485331227f0064e887068124fac1a8cb96d7a8a'
            'd5f6e7fd4eba1e562bd10861417cd1b79451d33da25f142a1a10127523a24ff4')

prepare() {
  cp -R "enumGen-${_enumGenver}" "$_pkgname-$pkgver/subprojects"
  cd "$_pkgname-$pkgver"
  patch -Np1 -i ../qscan-gcc13-fix.patch
}

build() {
  cd "$_pkgname-$pkgver"

  arch-meson build -Dwerror=false
  meson compile -C build
}

package() {
  cd "$_pkgname-$pkgver"

  meson install -C build --destdir "$pkgdir"
}

# REMINDER: makepkg --printsrcinfo > .SRCINFO
