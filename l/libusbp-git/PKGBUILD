# Maintainer: Sebastian Baberowski <sebastian@baberowski.com>

pkgname=libusbp-git
pkgver=1.3.1.r0.g1d73942
pkgrel=1
pkgdesc="Pololu USB Library (also known as libusbp) is a cross-platform C library for accessing USB devices"
url="https://github.com/pololu/libusbp"
license=(MIT)
arch=(x86_64 i686)
depends=(glibc)
makedepends=(cmake git)
provides=(libusbp)
conflicts=(libusbp libusbp-1)
replaces=(libusbp-1)
source=("git+https://github.com/pololu/libusbp.git")
sha256sums=(SKIP)

pkgver() {
  cd libusbp
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "libusbp" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 libusbp/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
