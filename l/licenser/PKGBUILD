# Maintainer: Mohammad Ashar Khan <ashar786khan@gmail.com>

pkgname=licenser
pkgver=0.50
pkgrel=2
pkgdesc='A License management tool for your Open Source projects.'
arch=('x86_64')
url='https://github.com/coder3101/licenser'
license=('GPL3')
depends=()
makedepends=("cmake" "git" clang)
source=("git+https://github.com/coder3101/licenser.git#tag=${pkgver}"
        "git+https://github.com/muellan/clipp.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
 cd licenser
  git submodule init
  git config submodule.third_party/clipp.url "${srcdir}/clipp"
  git -c protocol.file.allow=always submodule update
}

build() {
  cmake -S licenser -Bbuild \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_CXX_COMPILER=clang++ \
	  -DCMAKE_C_COMPILER=clang \
	  -DENABLE_TESTS=off
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
