# Maintainer: Sebastian Baberowski <sebastian@baberowski.com>

pkgname=libusbp
pkgver=1.3.1
pkgrel=1
pkgdesc="Pololu USB Library (also known as libusbp) is a cross-platform C library for accessing USB devices"
url="https://github.com/pololu/libusbp"
license=(MIT)
arch=(x86_64 i686)
depends=(glibc)
makedepends=(cmake git)
conflicts=(libusbp-1)
replaces=(libusbp-1)
source=("git+https://github.com/pololu/libusbp.git#tag=${pkgver}")
sha256sums=('6372d6abacf8fb0b129f040c34b21e490bd2dc4720ce841f09f25dfc2a833b3b')

build() {
  cmake -B build -S "libusbp" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 libusbp/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
