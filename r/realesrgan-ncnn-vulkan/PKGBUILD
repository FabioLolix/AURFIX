# Maintainer: HurricanePootis <hurricanepootis@protonmail.com>

pkgname=realesrgan-ncnn-vulkan
_pkgname=Real-ESRGAN-ncnn-vulkan
pkgver=0.2.0
pkgrel=7
pkgdesc="NCNN implementation of Real-ESRGAN"
arch=('x86_64')
url="https://github.com/xinntao/Real-ESRGAN-ncnn-vulkan"
license=('MIT')
depends=('vulkan-driver' 'libwebp' 'ncnn')
makedepends=('git' 'cmake' 'vulkan-headers' 'glslang')
source=(
    "git+https://github.com/xinntao/$_pkgname#tag=v${pkgver}"
    "https://github.com/xinntao/Real-ESRGAN/releases/download/v0.2.5.0/realesrgan-ncnn-vulkan-20220424-ubuntu.zip"
    "realesrgan-ncnn-vulkan.sh"
    )
sha256sums=('SKIP'
            'e5aa6eb131234b87c0c51f82b89390f5e3e642b7b70f2b9bbe95b6a285a40c96'
            '058bc5167a00ff53a6a135ed033797a4d012bdb86e930ba4eb271ab4c848f8df')

build() {
    cmake -B build -S "$_pkgname/src" \
    -DBUILD_SHARED_LIBS=0 \
    -DUSE_SYSTEM_NCNN=1 \
    -DUSE_SYSTEM_WEBP=1 \
    -DGLSLANG_TARGET_DIR=/usr/lib/cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev

    cmake --build build
}

package() {
    install -Dm755 build/realesrgan-ncnn-vulkan "$pkgdir/usr/lib/realesrgan-ncnn-vulkan/realesrgan-ncnn-vulkan"

    install -Dm755 realesrgan-ncnn-vulkan.sh "$pkgdir/usr/bin/realesrgan-ncnn-vulkan"

    mkdir -p "$pkgdir/usr/share/realesrgan-ncnn-vulkan/"
    cp -r models "$pkgdir/usr/share/realesrgan-ncnn-vulkan/models"

    install -Dm644 "$srcdir/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
