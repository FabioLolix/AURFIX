# Maintainer: Core_UK <mail.coreuk@gmail.com>

pkgname=cider-dev-git
_pkgname=Cider
pkgver=r4055.2b048798
pkgrel=1
pkgdesc="Project Cider. An open-source Apple Music client built from the ground up with Vue.js and Electron. Compiled from the GitHub repositories stable branch."
arch=(x86_64 armv7h i686)
url="https://cider.sh/"
license=(AGPL3)
depends=(gtk3 nss alsa-lib)
makedepends=(git npm yarn)
optdepends=('libnotify: Playback notifications')
provides=(cider)
conflicts=(cider)
source=("git+https://github.com/CiderApp/${_pkgname}.git#branch=main"
        cider.desktop)
sha256sums=('SKIP'
            'c41e9b1019411019fce8509e32f770edf33c9e864bf707c30ffe2e3f2dcf1571')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_pkgname}"

    if [ -f cider.lock ]; then
        mv cider.lock yarn.lock
    fi


    yarn install --non-interactive --pure-lockfile --cache-folder "${srcdir}/yarn-cache"

    if [[ ${CARCH} == "armv7h" ]]; then
        yarn build
        yarn electron-builder build --armv7l --linux dir
    elif [[ ${CARCH} == "i686" ]]; then
        yarn build
        yarn electron-builder build --ia32 --linux dir
    elif [[ ${CARCH} == "x86_64" ]]; then
        yarn build
        yarn electron-builder build --x64 --linux dir
    fi
}

package() {
    cd "${srcdir}/${_pkgname}"

    install -Dm644 "${srcdir}/cider.desktop" "${pkgdir}/usr/share/applications/cider.desktop"

    install -Dm644 "${srcdir}/${_pkgname}/resources/icons/icon.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/cider.png"

    install -d "$pkgdir/opt/"
    install -d "$pkgdir/usr/bin/"
    cd ${srcdir}/${_pkgname}/dist/linux-unpacked
    mkdir "${pkgdir}/opt/cider"
    cp -r --preserve=mode * "${pkgdir}/opt/cider"
    ln -sf "/opt/cider/cider" "${pkgdir}/usr/bin/cider"

    install -Dm644 "${srcdir}/${_pkgname}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
