# Maintainer: SebRmv <sbriais at-symbol free dot fr>

pkgname=omake-git
pkgver=0.10.7.r2.g0707479
pkgrel=1
pkgdesc="A build system designed for scalability and portability"
arch=(x86_64)
url="https://github.com/ocaml-omake/omake"
license=(GPL-2.0-only)
depends=(glibc ncurses readline)
makedepends=(git ocaml)
provides=(omake)
conflicts=(omake)
source=("git+https://github.com/ocaml-omake/omake.git")
sha256sums=('SKIP')

pkgver() {
  cd "omake"
  git describe --long --tags --abbrev=7 | sed 's/^omake-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "omake"
  ./configure -prefix=/usr
  make
}

package() {
  cd "omake"
  make INSTALL_ROOT="$pkgdir" install
  mkdir -vp "$pkgdir"/usr/share/
  mv -v "$pkgdir"/usr/man "$pkgdir"/usr/share/
}
