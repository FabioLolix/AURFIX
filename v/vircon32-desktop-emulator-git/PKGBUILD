# Maintainer: Javier Carracedo <https://github.com/vircon32>
# Contributor: DeathKhan <jfoh96@gmail.com>
# Contributor: Chandler Kl√ºser <chandler.kluser@gmail.com>

pkgname=vircon32-desktop-emulator-git
pkgver=23.6.4.r36.g24770a4
pkgrel=3
pkgdesc="Vircon32 Desktop Emulator"
arch=(x86_64)
url="http://www.vircon32.com/"
license=(BSD)
depends=(sdl2 sdl2_image openal freealut tinyxml2 gtk2) #mesa
makedepends=(git cmake)
provides=(vircon32-desktop-emulator)
conflicts=(vircon32-desktop-emulator)
source=("vircon32::git+https://github.com/vircon32/ComputerSoftware.git")
sha256sums=('SKIP')

pkgver() {
  cd vircon32
  git describe --long --tags | sed 's/^emulator-v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S vircon32/DesktopEmulator
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -D vircon32/DesktopEmulator/Data/Readme.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
