# Maintainer: Sven Rademakers <sven@turingpi.com>:Ruslan Akbashev <ra@turingpi.com>

pkgname=tpi
pkgver=1.0.7
pkgrel=2
pkgdesc='Official Turing-Pi2 CLI tool'
arch=(x86_64 aarch64 armv7h)
url=https://turingpi.com/
license=(Apache-2.0)
makedepends=(cargo)
options=(!lto)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/turing-machines/tpi/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('866e2bd69d479e32b4c36e8378b239b0183a91fa6ff83272ea979f5b973f9edc')

prepare() {
    cd "tpi-${pkgver}"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cd "tpi-${pkgver}"
    cargo build --frozen --release 
}

# 0 test to run
#check() {
#    export RUSTUP_TOOLCHAIN=stable
#    cd "tpi-${pkgver}"
#    cargo test --frozen 
#}

package() {
    install -Dm0755 -t "$pkgdir/usr/bin/" "$srcdir/tpi-${pkgver}/target/release/tpi"
}
