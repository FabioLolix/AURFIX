# Maintainer: Matthias Baur <aur@matthiasbaur.me>
pkgname=noson-app
pkgver=5.6.8
pkgrel=1
pkgdesc="The essential to control music from your SONOS devices on Linux platforms"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://janbar.github.io/noson-app"
license=(GPL-3.0-only)
depends=('qt5-base' 'qt5-quickcontrols2' 'qt5-graphicaleffects' 'qt5-svg' 'libpulse' 'flac')
makedepends=('cmake' 'git' noson)
source=("git+https://github.com/janbar/noson-app.git#tag=${pkgver}")
sha256sums=('4208ce9f2421e0de92baca696178aab524ef2497095f11a1626fb78b4fbf277a')

build() {
  local _flags=(
    -DBUILD_LIBNOSON:BOOL=OFF
  )

  cmake -B build -S "noson-app" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
