# Maintainer: necklace <ns@nsz.no>

pkgname=grok-jpeg2000
pkgver=14.3.0
pkgrel=1
pkgdesc="A high performance open source JPEG 2000 codec."
arch=(x86_64)
url="https://github.com/GrokImageCompression/grok"
license=(AGPL-3.0-only)
depends=(libpng libtiff lcms2 libjpeg perl-image-exiftool)
makedepends=(cmake cli11 doxygen python-sphinx)
source=("$pkgname-$pkgver.tar.gz::https://github.com/GrokImageCompression/grok/archive/v$pkgver.tar.gz")
sha256sums=('832fcd6bc5bd2ce430373c3206f03097d265e4ffce55b7ceb4e27733fdcafbf4')

build() {
  local _flags=(
    -DBUILD_SHARED_LIBS=ON
    -DBUILD_TESTING=OFF
    -DGRK_BUILD_DOC=ON
    -DGRK_BUILD_JPEG=OFF
    -DGRK_BUILD_LCMS2=OFF
    -DGRK_BUILD_LIBPNG=OFF
    -DGRK_BUILD_LIBTIFF=OFF
  )

  cmake -B build -S "grok-${pkgver}" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
