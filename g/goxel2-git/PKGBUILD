# Maintainer: Aditya Mishra <https://github.com/pegvin/goxel2/issues>
pkgname=goxel2-git
pkgver=0.13.2.r1.gfa3e2ae
pkgrel=1 # Update if you changed something but that is so minor change you don't want to change the version
pkgdesc="a cross-platform 3d voxel art editor"
arch=('i686' 'x86_64')
url="https://github.com/pegvin/goxel2"
license=('GPL3')
depends=(glfw gtk3)
makedepends=(git scons)
provides=(goxel2)
conflicts=(goxel2)
source=("git+https://github.com/pegvin/goxel2.git")
sha256sums=('SKIP')

pkgver() {
    cd goxel2
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd goxel2
	# don't fail on warnings:
	CFLAGS="${CFLAGS} -Wno-all"
	CXXFLAGS="${CFLAGS}"

	make release
}

package() {
    cd goxel2
	install -Dm644 data/goxel2.desktop "$pkgdir/usr/share/applications/goxel2.desktop"
	install -Dm644 icon.png "$pkgdir/usr/share/icons/goxel2.png"
	install -Dm755 goxel2 "$pkgdir/usr/bin/goxel2"
}
