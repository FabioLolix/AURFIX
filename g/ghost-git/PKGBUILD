# Maintainer: z3oxs <z3ox1s@protonmail.com>

pkgname=ghost-git
pkgver=1.1.0.r0.g6becb65
pkgrel=1
pkgdesc="Simple tool to take screenshots using your terminal"
arch=(x86_64 i686)
url="https://github.com/z3oxs/ghost"
license=(MIT)
depends=(cairo libxi libxinerama libxrandr libxcursor libglvnd)
makedepends=(git go)
provides=(ghost)
conflicts=(ghost)
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/ghost"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/ghost"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
        .
}

package() {
    cd "$srcdir/ghost"
    install -Dm755 ghost "${pkgdir}/usr/bin/ghost"
    install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
