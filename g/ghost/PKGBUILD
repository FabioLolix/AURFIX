# Maintainer: z3oxs <z3ox1s@protonmail.com>

pkgname=ghost
pkgver=1.1.0
pkgrel=1
pkgdesc="Simple tool to take screenshots using your terminal"
arch=(x86_64 i686)
url="https://github.com/z3oxs/ghost"
license=(MIT)
depends=(cairo libxi libxinerama libxrandr libxcursor libglvnd)
makedepends=(git go)
source=("git+$url.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
    cd "$srcdir/ghost"
    go build \
        -trimpath \
        -buildmode=pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
        .
}

package() {
    cd "$srcdir/ghost"
    install -Dm755 ghost "${pkgdir}/usr/bin/ghost"
    install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
