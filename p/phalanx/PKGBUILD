# Maintainer: Eric Ozwigh <ozwigh at gmail dot com>
# Contributor: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>
# Patches from phalanx @ openSUSE build service

pkgname=phalanx
pkgver=25
_pkgver=XXV
pkgrel=1
pkgdesc="A chess engine which understands the xboard protocol. It's suitable for beginner and intermediate players"
url="https://sourceforge.net/projects/phalanx/"
license=(GPL-2.0-only)
arch=(x86_64 i686)
optdepends=('xboard: to be able to use the xboard graphical frontend')
source=("https://sourceforge.net/projects/phalanx/files/Version%20${_pkgver}/phalanx-${_pkgver}-source.tgz"
        "phalanx-23.diff"
        "phalanx-castling-broken.patch")
md5sums=('b8744eba0b85371ec36d855693b4b72c'
         '175491780d998dfb55d4315c3df0623d'
         '9b60d4fbad709aad8ca0e9ff655b9da6')
sha512sums=('655214820dbd6605cbf035bdb8a10bb6b7dceaa6ce081a3a0966d67d146683a63eb1610fa65d32d3e95c07f94b107acda178a10225bca93e4304bee26f33d59a'
            '874d632efa3564f75a5534d0b2cb696e72b6a94e118e85dd590a1a79860ee1c18faa75da22df73d5774144661c3cb55ab8b9484a1a48bca76b954e484d8d3bcd'
            'f7b007ee952fe96a26da433845e3b6babae78d1ed6a70e41d07ef213a9f008b920c715033d44f6d4d11a7771e7b5dd653877f55b918b954b68062cf166ad0d08')

prepare () {
   # Use the makepkg CFLAGS set for building phalanx
   cd "Phalanx-${_pkgver}"
   sed -i "s/CFLAGS = -O0/# CFLAGS = -O0/g" makefile
   # Apply patches
   #patch < "${srcdir}"/phalanx-23.diff
   patch < "${srcdir}"/phalanx-castling-broken.patch
}

build() {
   cd "Phalanx-${_pkgver}"
   make
}

package () {
  cd "Phalanx-${_pkgver}"
  #make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -t "${pkgdir}"/usr/bin -D phalanx
  install -t "${pkgdir}"/usr/share/phalanx -D -m0644 eco.phalanx pbook.phalanx sbook.phalanx
}
