# Maintainer: nbebaw <nader.bebawy@gmx.de>
pkgname=archub-git
pkgver=0.0.2.r0.g58380b6
pkgrel=1
pkgdesc="Command-line tool designed for managing packages in AUR"
arch=(x86_64)
url="https://github.com/nbebaw/Archub"
license=(MIT)
depends=(git)
makedepends=(go)
provides=(archub)
conflicts=(archub)
source=("git+https://github.com/nbebaw/Archub.git")
sha256sums=('SKIP')

pkgver() {
  cd "Archub"
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "Archub"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd "Archub"
  install -Dm755 archub -t "${pkgdir}/usr/bin/"
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
