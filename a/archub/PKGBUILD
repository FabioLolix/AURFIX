# Maintainer: nbebaw <nader.bebawy@gmx.de>
pkgname=archub
pkgver=0.0.2
pkgrel=1
pkgdesc="Command-line tool designed for managing packages in AUR"
arch=(x86_64)
url="https://github.com/nbebaw/Archub"
license=(MIT)
depends=(git)
makedepends=(go)
source=("archub-${pkgver}.tar.gz::https://github.com/nbebaw/Archub/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('7c171dd82620d5edca72d82c4840ba0b140421901b0e8b4a905e5e21fb7c5941')

build() {
  cd "Archub-${pkgver}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd "Archub-${pkgver}"
  install -Dm755 archub -t "${pkgdir}/usr/bin/"
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
