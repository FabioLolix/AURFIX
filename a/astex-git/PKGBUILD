# Maintainer: Hurstel Alexandre <a.hurstel@unistra.fr>

_pkgname=ASTex
pkgname=astex-git
pkgver=r461.3c1e70c
pkgrel=1
pkgdesc="ASTex is an open-source library for texture analysis and synthesis."
arch=(i686 x86_64)
url="https://astex-icube.github.io"
license=(GPL)
depends=(insight-toolkit libpng openexr)
makedepends=(git cmake cgal glfw glu glew eigen boost gcc13)
provides=(astex)
conflicts=(astex)
options=(!lto)
source=("git+https://github.com/ASTex-ICube/ASTex.git")
b2sums=('SKIP')

pkgver() {
  cd "ASTex"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  export CC=/usr/bin/gcc-13 CXX=/usr/bin/g++-13
  export CFLAGS+=" -Wno-error=format-security"
  export CXXFLAGS+=" -Wno-error=format-security"

  cmake -B build -S "ASTex" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
